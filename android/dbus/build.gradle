apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionName project.pkg_version_name
        versionCode project.pkg_version_code.toInteger()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility = 1.8
    }
}

publishing {
    android.libraryVariants.all { variant ->
        variant.outputs.all { output ->
            publishing.publications.create(variant.name, MavenPublication) {
                artifactId project.pkg_artifact_id
                groupId project.pkg_name
                version project.pkg_version_name
                artifact variant.packageLibraryProvider.get().archivePath
                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.getByName(variant.name + "CompileClasspath")
                        .allDependencies
                        .findAll { it instanceof ExternalDependency }
                        .each {
                            def dependency = dependencies.appendNode('dependency')
                            dependency.appendNode('groupId', it.group)
                            dependency.appendNode('artifactId', it.name)
                            dependency.appendNode('version', it.version)
                        }
                }
            }
        }
    }
    repositories {
        maven {
            name 'Release'
            url = "$buildDir/maven/release"
        }
        maven {
            name 'Debug'
            url = "$buildDir/maven/debug"
        }
    }
    afterEvaluate {
        bundleDebugAar.finalizedBy publishDebugPublicationToDebugRepository
        bundleReleaseAar.finalizedBy publishReleasePublicationToReleaseRepository
    }
}

dependencies {
    annotationProcessor 'com.google.auto.service:auto-service:1.0'
    implementation 'com.google.auto.service:auto-service-annotations:1.0'
    implementation 'com.wyplay.tools:service-tools:1.+'
}
